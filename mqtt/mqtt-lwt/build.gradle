plugins {
    id 'org.springframework.boot' version '1.5.4.RELEASE'
}

apply plugin: 'org.springframework.boot'

description = "EnMasse MQTT LWT"

repositories {
     maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
     maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'io.vertx', name: 'vertx-proton', version:'3.4.2-SNAPSHOT'
    compile group: 'org.springframework.boot', name: 'spring-boot', version:'1.5.4.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version:'1.5.4.RELEASE'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.21'
    compile group: 'ch.qos.logback', name: 'logback-classic', version:'1.1.11'
    compile group: 'io.netty', name: 'netty-codec-mqtt', version:'4.1.8.Final'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-logging', version:'1.5.4.RELEASE'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'io.vertx', name: 'vertx-unit', version:'3.3.3'
}

task pack(type: Tar) {
    from ('build/libs') {
        include 'mqtt-lwt.jar'
    }
    from ('src/main/sh') {
        include 'run_mqtt.sh'
    }
    compression = Compression.GZIP
    archiveName "mqtt-lwt.tgz"
    destinationDir file('build')
}

task setupRouter(type: Exec) {
    commandLine 'docker', 'run', '-d', '--name', 'qdrouterd-lwt', '-p', '5673:5672', '-p', '55671:55671', '-p', '56671:56671', '-v', "${projectDir}/src/test/resources:/conf:z", 'ppatierno/qdrouterd:0.8.0-repo', 'qdrouterd', '--conf', '/conf/qdrouterd.conf'
}

task teardownRouter(type: Exec) {
    commandLine 'docker', 'stop', 'qdrouterd-lwt'
}

task teardownImage(type: Exec) {
    dependsOn teardownRouter
    commandLine 'docker', 'rm', 'qdrouterd-lwt'
}

test {
    dependsOn setupRouter
    finalizedBy teardownImage
    testLogging.showStandardStreams true
}
