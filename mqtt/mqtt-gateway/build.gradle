plugins {
    id 'org.springframework.boot' version '1.5.4.RELEASE'
}

apply plugin: 'org.springframework.boot'

description = "EnMasse MQTT gateway"

repositories {
     maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
     maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'io.vertx', name: 'vertx-mqtt-server', version:'3.4.2-SNAPSHOT'
    compile group: 'io.vertx', name: 'vertx-proton', version:'3.4.2-SNAPSHOT'
    compile group: 'org.springframework.boot', name: 'spring-boot', version:'1.5.4.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version:'1.5.4.RELEASE'
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.7.21'
    compile group: 'ch.qos.logback', name: 'logback-classic', version:'1.1.11'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-logging', version:'1.5.4.RELEASE'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'io.vertx', name: 'vertx-unit', version:'3.3.3'
    testCompile group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version:'1.1.0'
}

task setupRouter(type: Exec) {
    commandLine 'docker', 'run', '-d', '--name', 'qdrouterd', '-p', '5672:5672', '-p', '55673:55673', '-v', "${projectDir}/src/test/resources:/conf:z", 'ppatierno/qdrouterd:0.8.0-repo', 'qdrouterd', '--conf', '/conf/qdrouterd.conf'
}

task teardownRouter(type: Exec) {
    commandLine 'docker', 'stop', 'qdrouterd'
}

task teardownImage(type: Exec) {
    dependsOn teardownRouter
    commandLine 'docker', 'rm', 'qdrouterd'
}

test.dependsOn setupRouter
test.finalizedBy teardownImage
clean.dependsOn teardownImage

task pack(type: Tar) {
    from ('build/libs') {
        include 'mqtt-gateway.jar'
    }
    from ('src/main/sh') {
        include 'run_mqtt.sh'
    }
    compression = Compression.GZIP
    archiveName "mqtt-gateway.tgz"
    destinationDir file('build')
}
